filename : "GenerateParser" @Color("FileName");
include : "Include/stdafx.h";
namespace : "ztl";
namespace : "general_parser";
dirname : "General";
		
		class GeneralTypeDefine @Color("asas","bbb")
		{

		};
		struct GeneralAttributeArgumentDefine
		{
			token name @Color("AttributeArgumentName");
		};
		struct GeneralAttributeDefine 
		{
			token name @Color("AttributeName");
			GeneralAttributeArgumentDefine[] arguments;
		};
		struct GeneralHeadInfoDefine
		{
			token    property @Color("HeadInfoName");
			token    value;
			GeneralAttributeDefine[] attributes;
		};
		
		class GeneralTokenDefine
		{
			enum TokenOptional
			{
				False,
				True,
			};
			token			name @Color("TokenName");
			token			regex;
			TokenOptional	ignore;
			GeneralAttributeDefine[] attributes;
		};

	
		class GeneralTypeObject
		{

		};

		class GeneralArrayTypeObject :  GeneralTypeObject
		{
			GeneralTypeObject			element;
		};
	
		class GeneralTokenTypeObject :  GeneralTypeObject
		{

		};
		class GeneralNormalTypeObject :  GeneralTypeObject
		{
			token									name;
		};
		class GeneralSubTypeObject :  GeneralTypeObject
		{
			GeneralTypeObject						parent;
			token									name;
		};
		
	
		class GeneralClassMemberTypeDefine :  GeneralTypeDefine
		{
			GeneralTypeObject						   type;
			token									name @Color("ClassFieldName");
			GeneralAttributeDefine[] attributes;
		};
		class GeneralEnumMemberTypeDefine :  GeneralTypeDefine
		{
			token									name @Color("EnumFieldName");
			GeneralAttributeDefine[] attributes;
		};
		class GeneralClassTypeDefine :  GeneralTypeDefine
		{
			GeneralTypeDefine[]				subTypes;
			GeneralClassMemberTypeDefine[]	members;
			GeneralTypeObject				parent;
			token						name @Color("ClassName");
			GeneralAttributeDefine[] attributes;
		};
		class GeneralEnumTypeDefine :  GeneralTypeDefine
		{
			GeneralEnumMemberTypeDefine[]			members;
			token								name @Color("EnumName");
			GeneralAttributeDefine[] attributes;
		};
		class GeneralGrammarTypeDefine 
		{

		};
		class GeneralGrammarTextTypeDefine :  GeneralGrammarTypeDefine
		{
			token												text;
		};
		class GeneralGrammarNormalTypeDefine :  GeneralGrammarTypeDefine
		{
			token												name;
		};
		class GeneralGrammarSequenceTypeDefine :  GeneralGrammarTypeDefine
		{
			GeneralGrammarTypeDefine				first;
			GeneralGrammarTypeDefine				second;
		};
		class GeneralGrammarLoopTypeDefine :  GeneralGrammarTypeDefine
		{
			GeneralGrammarTypeDefine				grammar;
		};
		class GeneralGrammarOptionalTypeDefine :  GeneralGrammarTypeDefine
		{
			GeneralGrammarTypeDefine				grammar;
		};
		class GeneralGrammarAssignTypeDefine :  GeneralGrammarTypeDefine
		{
			token									name;
			GeneralGrammarTypeDefine				grammar;
		};
		
		class GeneralGrammarSetterTypeDefine :  GeneralGrammarTypeDefine
		{
			GeneralGrammarTypeDefine grammar;
			token												name;
			token												value;
		};
		class GeneralGrammarUsingTypeDefine :  GeneralGrammarTypeDefine
		{
			GeneralGrammarTypeDefine				grammar;
		};
		class GeneralGrammarCreateTypeDefine :  GeneralGrammarTypeDefine
		{
			GeneralTypeObject						type;
			GeneralGrammarTypeDefine				grammar;
		};
		
		class GeneralGrammarAlternationTypeDefine : GeneralGrammarTypeDefine
		{
			GeneralGrammarTypeDefine left;
			GeneralGrammarTypeDefine right;
		};
		class GeneralRuleDefine
		{
			GeneralTypeObject						type;
			token								name @Color("RuleName");
			GeneralGrammarTypeDefine[]				grammars;
			GeneralAttributeDefine[] attributes;
		};
		class GeneralTableDefine
		{
			GeneralHeadInfoDefine[] heads;
			GeneralTypeDefine[]   types;
			GeneralTokenDefine[]  tokens;
			GeneralRuleDefine[]   rules;
		};

token CLASS = "class" @Color("Keywords");
token STRUCT = "struct"@Color("Keywords");
token ENUM = "enum"@Color("Keywords");
token TOKEN = "token"@Color("Keywords");
token DISCARDTOKEN = "ignoretoken"@Color("Keywords");
token RULE = "rule"@Color("Keywords");
token AS = "as"@Color("Keywords");
token WITH = "with"@Color("Keywords");
token OPEN = "\{";
token CLOSE = "\}";
token SEMICOLON = ";";
token COLON = ":";
token COMMA = ",";
token DOT = "\.";
token ASSIGN = "=";
token USING = "!";
token OR = "\|";
token ATSIGN = "@";
token OPTOPEN = "\[";
token OPTCLOSE = "\]";
token PREOPEN = "\(";
token PRECLOSE = "\)";
token NAME = "[a-zA-Z_]\w*";
token STRING = "\"(\\\\|\\\"|[^\"])*\"" @Color("String");
ignoretoken SPACE = "\s+";
ignoretoken LINENOTE = "//[^\n]*\n"@Color("Note");
ignoretoken BLOCKNOTE = "/\*.*?\*/"@Color("Note");

rule GeneralAttributeArgumentDefine AttributeArgument 
        = STRING : name as GeneralAttributeArgumentDefine
        ;
rule GeneralAttributeDefine Attribute
        = "@" NAME : name "(" [ AttributeArgument : arguments ] { "," AttributeArgument : arguments } ")" as GeneralAttributeDefine
        ;
rule GeneralTypeDefine TypeDecl
        = !Enum | !Class
        ;
rule GeneralTypeObject Type
        = NAME : name as GeneralNormalTypeObject
        = "token" as GeneralTokenTypeObject
        = Type : parent "." NAME : name as GeneralSubTypeObject 
        = Type : element "[" "]" as GeneralArrayTypeObject
        ;
rule GeneralEnumMemberTypeDefine EnumMember
        = NAME : name [ Attribute : attributes ] { "," Attribute : attributes } "," as GeneralEnumMemberTypeDefine 
        ;
rule GeneralEnumTypeDefine Enum
        = "enum" NAME : name [ Attribute : attributes]  { "," Attribute : attributes }  "{" { EnumMember : members } "}" ";" as GeneralEnumTypeDefine
        ;
rule GeneralClassMemberTypeDefine ClassMember
        = Type : type NAME : name [ Attribute : attributes]  { "," Attribute : attributes }  ";" as GeneralClassMemberTypeDefine
        ;


rule GeneralClassTypeDefine Class
        = ("struct" |"class") NAME : name [ ":" Type : parent ] [ Attribute : attributes  ] { "," Attribute : attributes }  "{" { ClassMember : members | TypeDecl : subTypes } "}" ";" as GeneralClassTypeDefine
        ;
rule GeneralGrammarTypeDefine SequenceGrammar
        = !PrimitiveGrammar
        = PrimitiveGrammar : first SequenceGrammar : second as GeneralGrammarSequenceTypeDefine
        ;
rule GeneralGrammarTypeDefine AlternativeGrammar
        = !SequenceGrammar
        = SequenceGrammar  : left "|" AlternativeGrammar : right as GeneralGrammarAlternationTypeDefine
        ;

rule GeneralGrammarTypeDefine Grammar
        = !AlternativeGrammar
        = Grammar : grammar "as" Type : type as GeneralGrammarCreateTypeDefine
        = Grammar : grammar "with" "{" NAME : name "=" STRING : value  "}"  as GeneralGrammarSetterTypeDefine
		;
rule GeneralGrammarTypeDefine PrimitiveGrammar
        =  NAME : name as GeneralGrammarNormalTypeDefine
		=  STRING : text as GeneralGrammarTextTypeDefine
		= PrimitiveGrammar : grammar ":" NAME : name as GeneralGrammarAssignTypeDefine
        = "!" PrimitiveGrammar : grammar as GeneralGrammarUsingTypeDefine
        = "[" Grammar : grammar "]" as GeneralGrammarOptionalTypeDefine
        = "{" Grammar : grammar "}" as GeneralGrammarLoopTypeDefine
		=  "(" !Grammar ")"
        ;

rule GeneralTokenDefine TokenDecl
        = "token" NAME : name "=" STRING : regex [ Attribute : attributes ]{ "," Attribute : attributes }  ";" as GeneralTokenDefine with { ignore = "False" }
        = "ignoretoken" NAME : name "=" STRING : regex [ Attribute : attributes ]{ "," Attribute : attributes } ";" as GeneralTokenDefine with { ignore = "True" }
        ;
rule GeneralHeadInfoDefine HeadDecl = NAME : property ":" STRING : value [ Attribute : attributes  ] { "," Attribute : attributes } ";" as GeneralHeadInfoDefine
		;
rule GeneralRuleDefine RuleDecl
        = "rule" Type : type NAME : name [ Attribute : attributes ]{ "," Attribute : attributes }   { "=" Grammar : grammars } ";" as GeneralRuleDefine
        ;
rule GeneralTableDefine ParserDecl
        = {HeadDecl : heads}| { TypeDecl : types} |{ TokenDecl : tokens} |{ RuleDecl : rules } as GeneralTableDefine
        ;
