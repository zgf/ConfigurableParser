filename : "GenerateParser";
include : "Include/stdafx.h";
namespace : "ztl";
namespace : "general_parser";
dirname : "General";

		struct GeneralHeadInfoDefine
		{
			token    property;
			token    value;
		};
		
		class GeneralTokenDefine
		{
			enum TokenOptional
			{
				False,
				True,
			};
			token			name;
			token			regex;
			TokenOptional	ignore;
		};

	
		class GeneralTypeObject
		{

		};

		class GeneralArrayTypeObject :  GeneralTypeObject
		{
			GeneralTypeObject			element;
		};
	
		class GeneralTokenTypeObject :  GeneralTypeObject
		{

		};
		class GeneralNormalTypeObject :  GeneralTypeObject
		{
			token									name;
		};
		class GeneralSubTypeObject :  GeneralTypeObject
		{
			GeneralTypeObject						parent;
			token									name;
		};
		
		class GeneralTypeDefine
		{

		};
		
		class GeneralClassMemberTypeDefine :  GeneralTypeDefine
		{
			GeneralTypeObject						   type;
			token									name;
		};
		class GeneralEnumMemberTypeDefine :  GeneralTypeDefine
		{
			token									name;
		};
		class GeneralClassTypeDefine :  GeneralTypeDefine
		{
			GeneralTypeDefine[]				subTypes;
			GeneralClassMemberTypeDefine[]	members;
			GeneralTypeObject				parent;
			token						name;
		};
		class GeneralEnumTypeDefine :  GeneralTypeDefine
		{
			GeneralEnumMemberTypeDefine[]			members;
			token								name;
		};
		class GeneralGrammarTypeDefine 
		{

		};
		class GeneralGrammarTextTypeDefine :  GeneralGrammarTypeDefine
		{
			token												text;
		};
		class GeneralGrammarNormalTypeDefine :  GeneralGrammarTypeDefine
		{
			token												name;
		};
		class GeneralGrammarSequenceTypeDefine :  GeneralGrammarTypeDefine
		{
			GeneralGrammarTypeDefine				first;
			GeneralGrammarTypeDefine				second;
		};
		class GeneralGrammarLoopTypeDefine :  GeneralGrammarTypeDefine
		{
			GeneralGrammarTypeDefine				grammar;
		};
		class GeneralGrammarOptionalTypeDefine :  GeneralGrammarTypeDefine
		{
			GeneralGrammarTypeDefine				grammar;
		};
		class GeneralGrammarAssignTypeDefine :  GeneralGrammarTypeDefine
		{
			token									name;
			GeneralGrammarTypeDefine				grammar;
		};
		
		class GeneralGrammarSetterTypeDefine :  GeneralGrammarTypeDefine
		{
			GeneralGrammarTypeDefine grammar;
			token												name;
			token												value;
		};
		class GeneralGrammarUsingTypeDefine :  GeneralGrammarTypeDefine
		{
			GeneralGrammarTypeDefine				grammar;
		};
		class GeneralGrammarCreateTypeDefine :  GeneralGrammarTypeDefine
		{
			GeneralTypeObject						type;
			GeneralGrammarTypeDefine				grammar;
		};
		
		class GeneralGrammarAlternationTypeDefine : GeneralGrammarTypeDefine
		{
			GeneralGrammarTypeDefine left;
			GeneralGrammarTypeDefine right;
		};
		class GeneralRuleDefine
		{
			GeneralTypeObject						type;
			token								name;
			GeneralGrammarTypeDefine[]				grammars;
		};
		class GeneralTableDefine
		{
			GeneralHeadInfoDefine[] heads;
			GeneralTypeDefine[]   types;
			GeneralTokenDefine[]  tokens;
			GeneralRuleDefine[]   rules;
		};

token CLASS = "class";
token STRUCT = "struct";
token ENUM = "enum";
token TOKEN = "token";
token DISCARDTOKEN = "ignoretoken";
token RULE = "rule";
token AS = "as";
token WITH = "with";
token OPEN = "\{";
token CLOSE = "\}";
token SEMICOLON = ";";
token COLON = ":";
token COMMA = ",";
token DOT = "\.";
token ASSIGN = "=";
token USING = "!";
token OR = "\|";
token OPTOPEN = "\[";
token OPTCLOSE = "\]";
token PREOPEN = "\(";
token PRECLOSE = "\)";
token NAME = "[a-zA-Z_]\w*";
token STRING = "\"(\\\"|[^\"])*\"";
ignoretoken SPACE = "\s+";
ignoretoken LINENOTE = "//[^\n]*\n";
ignoretoken BLOCKNOTE = "/\*.*?\*/";

rule GeneralTypeObject Type
        = NAME : name as GeneralNormalTypeObject
        = "token" as GeneralTokenTypeObject
        = Type : parent "." NAME : name as GeneralSubTypeObject 
        = Type : element "[" "]" as GeneralArrayTypeObject
        ;
rule GeneralEnumMemberTypeDefine EnumMember
        = NAME : name "," as GeneralEnumMemberTypeDefine 
        ;
rule GeneralEnumTypeDefine Enum
        = "enum" NAME : name  "{" { EnumMember : members } "}" ";" as GeneralEnumTypeDefine
        ;
rule GeneralClassMemberTypeDefine ClassMember
        = Type : type NAME : name ";" as GeneralClassMemberTypeDefine
        ;
rule GeneralTypeDefine TypeDecl
        = !Enum | !Class
        ;

rule GeneralClassTypeDefine Class
        = ("struct" |"class") NAME : name [ ":" Type : parent ] "{" { ClassMember : members | TypeDecl : subTypes } "}" ";" as GeneralClassTypeDefine
        ;
rule GeneralGrammarTypeDefine SequenceGrammar
        = !PrimitiveGrammar
        = SequenceGrammar : first PrimitiveGrammar : second as GeneralGrammarSequenceTypeDefine
        ;
rule GeneralGrammarTypeDefine AlternativeGrammar
        = !SequenceGrammar
        = AlternativeGrammar : left "|" SequenceGrammar : right as GeneralGrammarAlternationTypeDefine
        ;

rule GeneralGrammarTypeDefine Grammar
        = !AlternativeGrammar
        = Grammar : grammar "as" Type : type as GeneralGrammarCreateTypeDefine
        = Grammar : grammar "with" "{" NAME : name "=" STRING : value  "}"  as GeneralGrammarSetterTypeDefine
		;
rule GeneralGrammarTypeDefine NormalGrammar
        = NAME : name as GeneralGrammarNormalTypeDefine
		;

rule GeneralGrammarTypeDefine AssginGrammar
		= NormalGrammar : grammar ":" NAME : name as GeneralGrammarAssignTypeDefine
		= STRING : text as GeneralGrammarTextTypeDefine
		= "(" !Grammar ")"
		;

rule GeneralGrammarTypeDefine PrimitiveGrammar
        =  !AssginGrammar
        = "!" NormalGrammar : grammar as GeneralGrammarUsingTypeDefine
        = "[" Grammar : grammar "]" as GeneralGrammarOptionalTypeDefine
        = "{" Grammar : grammar "}" as GeneralGrammarLoopTypeDefine
        ;

rule GeneralTokenDefine TokenDecl
        = "token" NAME : name "=" STRING : regex ";" as GeneralTokenDefine with { ignore = "False" }
        = "ignoretoken" NAME : name "=" STRING : regex ";" as GeneralTokenDefine with { ignore = "True" }
        ;
rule GeneralHeadInfoDefine HeadDecl = NAME : property ":" STRING : value ";" as GeneralHeadInfoDefine
		;
rule GeneralRuleDefine RuleDecl
        = "rule" Type : type NAME : name { "=" Grammar : grammars } ";" as GeneralRuleDefine
        ;
rule GeneralTableDefine ParserDecl
        = {HeadDecl : heads}| { TypeDecl : types} |{ TokenDecl : tokens} |{ RuleDecl : rules } as GeneralTableDefine
        ;
